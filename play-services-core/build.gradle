/*
 * SPDX-FileCopyrightText: 2013 microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

configurations {
    withMapboxImplementation
    withVtmImplementation
    withNearbyImplementation
    withoutNearbyImplementation
}

dependencies {
    implementation "com.squareup.wire:wire-runtime:$wireVersion"
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    implementation project(':firebase-dynamic-links-api')
    implementation project(':firebase-auth-core')
    implementation project(':play-services-base-core')
    implementation project(':play-services-cast-core')
    implementation project(':play-services-cast-framework-core')
    implementation project(':play-services-conscrypt-provider-core')
    implementation project(':play-services-cronet-core')
    implementation project(':play-services-droidguard-core')
    implementation project(':play-services-fido-core')
    implementation project(':play-services-gmscompliance-core')
    implementation project(':play-services-location-core')
    withNearbyImplementation project(':play-services-nearby-core')
    implementation project(':play-services-oss-licenses-core')
    implementation project(':play-services-recaptcha-core')
    implementation project(':play-services-safetynet-core')
    implementation project(':play-services-tapandpay-core')
    implementation project(':play-services-vision-core')
    implementation project(':play-services-wearable-core')

    implementation project(':play-services-core-proto')

    implementation project(':play-services-core:microg-ui-tools') // deprecated
    implementation project(':play-services-api')

    implementation project(':play-services-appinvite')
    implementation project(':play-services-auth-base')
    implementation project(':play-services-auth')
    implementation project(':play-services-clearcut')
    implementation project(':play-services-drive')
    implementation project(':play-services-maps')
    implementation project(':play-services-measurement-base')
    implementation project(':play-services-places')
    implementation project(':play-services-recaptcha')
    implementation project(':play-services-safetynet')
    implementation project(':play-services-tasks-ktx')

    withMapboxImplementation project(':play-services-maps-core-mapbox')
    withVtmImplementation project(':play-services-maps-core-vtm')

    // AndroidX UI
    implementation "androidx.multidex:multidex:$multidexVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.mediarouter:mediarouter:$mediarouterVersion"
    implementation "androidx.preference:preference-ktx:$preferenceVersion"
    implementation "androidx.webkit:webkit:$webkitVersion"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    implementation "com.android.volley:volley:$volleyVersion"

    implementation "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
}

android {
    namespace "$applicationNamespace"

    compileSdkVersion androidCompileSdk
    buildToolsVersion "$androidBuildVersionTools"

    defaultConfig {
        applicationId "${basePackageName}.android.gms"

        versionName version
        versionCode appVersionCode

        minSdkVersion androidMinSdk
        targetSdkVersion androidTargetSdk

        multiDexEnabled true
        multiDexKeepProguard file('multidex-keep.pro')

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }

        buildConfigField "String", "APPLICATION_NAMESPACE", "\"${android.namespace}\""
        buildConfigField "String", "BASE_PACKAGE_NAME", "\"$basePackageName\""
        manifestPlaceholders = [
                basePackageName: "$basePackageName"
        ]

        // provide package name to xml resources
        resValue "string", "var_authenticator_account_type", "$basePackageName"
        resValue "string", "var_contact_syncadapter_account_type", "$basePackageName"
        resValue "string", "var_pref_auth_action_security_settings", "${basePackageName}.android.gms.accountsettings.SECURITY_SETTINGS"
        resValue "string", "var_pref_auth_action_privacy_settings", "${basePackageName}.android.gms.accountsettings.PRIVACY_SETTINGS"
        resValue "string", "var_pref_auth_action_preferences_settings", "${basePackageName}.android.gms.accountsettings.ACCOUNT_PREFERENCES_SETTINGS"
        resValue "string", "var_pref_start_gms_package", "${basePackageName}.android.gms"
    }

    buildFeatures {
        dataBinding = true
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
        withNearby {
            java.srcDirs += 'src/withNearby/kotlin'
        }
        withoutNearby {
            java.srcDirs += 'src/withoutNearby/kotlin'
        }
    }

    lintOptions {
        disable 'MissingTranslation', 'InvalidPackage', 'BatteryLife', 'ImpliedQuantity', 'MissingQuantity', 'InvalidWakeLockTag'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'maps', 'nearby'
    productFlavors {
        withMapbox {
            dimension 'maps'
        }
        withVtm {
            dimension 'maps'
            versionNameSuffix '-vtm'
        }
        withNearby {
            dimension 'nearby'
        }
        withoutNearby {
            dimension 'nearby'
            versionNameSuffix '-noen'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        jniLibs {
            useLegacyPackaging false
        }
    }
}

if (file('user.gradle').exists()) {
    apply from: 'user.gradle'
}
